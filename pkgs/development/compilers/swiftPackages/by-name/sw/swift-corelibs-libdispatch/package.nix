{
  lib,
  cmake,
  fetchFromGitHub,
  fetchpatch2,
  lld,
  ninja,
  stdenv,
  swift-minimal,
  swift-corelibs-libdispatch,
  swift_release,
  useSwift ? true,
}:

let
  swift-corelibs-libdispatch-no-overlay = swift-corelibs-libdispatch.override { useSwift = false; };

  swift-corelibs-libdispatch-no-overlay-lib =
    if useSwift then
      lib.escapeShellArg (lib.getLib swift-corelibs-libdispatch-no-overlay)
    else
      placeholder "out";

  swift-corelibs-libdispatch-no-overlay-dev =
    if useSwift then
      lib.escapeShellArg (lib.getDev swift-corelibs-libdispatch-no-overlay)
    else
      placeholder "dev";
in
stdenv.mkDerivation (finalAttrs: {
  pname = "swift-corelibs-libdispatch${lib.optionalString useSwift "-swift-overlay"}";
  version = swift_release;

  outputs = [
    "out"
    "dev"
    "man"
  ];

  src = fetchFromGitHub {
    owner = "swiftlang";
    repo = "swift-corelibs-libdispatch";
    tag = "swift-${finalAttrs.version}-RELEASE";
    hash = "sha256-Tu2G9FAP4q1xgM1n9q17T6VrqJ3tv8RezyUex38yNds=";
  };

  patches = [
    ./patches/0001-gnu-install-dirs.patch
    # Fixes `implicit conversion changes signedness` error.
    (fetchpatch2 {
      url = "https://github.com/swiftlang/swift-corelibs-libdispatch/commit/38872e2d44d66d2fb94186988509defc734888a5.patch?full_index=1";
      hash = "sha256-BXTv79ej93CBrHtEzHDu+3WkIfzEctwyqBoPkNQQkAA=";
    })
  ];

  strictDeps = true;

  # The Swift overlay is built separately using the no-overlay derivation as a base.
  cmakeFlags = [ (lib.cmakeBool "ENABLE_SWIFT" useSwift) ];

  env.NIX_CFLAGS_COMPILE = lib.optionalString stdenv.hostPlatform.isWindows "-fuse-ld=lld";

  nativeBuildInputs = [
    cmake
    ninja
  ]
  ++ lib.optionals useSwift [ swift-minimal ]
  ++ lib.optionals stdenv.hostPlatform.isWindows [ lld ];

  postInstall = ''
    libExt=${stdenv.hostPlatform.extensions.library}

    # Provide a CMake module. This is primarily used to glue together parts of
    # the Swift toolchain. Modifying the CMake config to do this for us is
    # otherwise more trouble.
    mkdir -p "''${!outputDev}/lib/cmake/dispatch"
    substitute ${./files/dispatchConfig.cmake} "''${!outputDev}/lib/cmake/dispatch/dispatchConfig.cmake" \
      --replace-fail '@buildType@' ${if stdenv.hostPlatform.isStatic then "STATIC" else "SHARED"} \
      --replace-fail '@swiftPlatform@' ${stdenv.hostPlatform.swift.platform} \
      --replace-fail '@lib@' ${swift-corelibs-libdispatch-no-overlay-lib} \
      --replace-fail '@dev@' ${swift-corelibs-libdispatch-no-overlay-dev} \
      --replace-fail '@out-swift@' "$out" \
      --replace-fail '@dev-swift@' "''${!outputDev}"
  ''
  + lib.optionalString useSwift ''
    mkdir -p "$dev/nix-support" "$man"

    moveToOutput lib/swift "''${!outputDev}"

    # Link the non-Swift dylibs into $out, so with and without Swift support uses the same libdispatch.
    rm "''${!outputLib}/lib/libdispatch$libExt" "''${!outputLib}/lib/libBlocksRuntime$libExt"
    for dylib in ${swift-corelibs-libdispatch-no-overlay-lib}/lib/*"$libExt"; do
      ln -s "$dylib" "''${!outputLib}/lib/$(basename "$dylib")"
    done

    ln -s ${lib.escapeShellArg (lib.getMan swift-corelibs-libdispatch-no-overlay)}/* "$man"

    echo -n ${swift-corelibs-libdispatch-no-overlay-dev} >> "$dev/nix-support/propagated-build-inputs"
  '';

  __structuredAttrs = true;

  meta = {
    description = "Grand Central Dispatch";
    homepage = "https://github.com/swiftlang/swift-corelibs-libdispatch";
    platforms = lib.platforms.freebsd ++ lib.platforms.linux ++ lib.platforms.windows;
    license = lib.licenses.asl20;
    maintainers = with lib.maintainers; [ cmm ];
    teams = [ lib.teams.swift ];
  };
})
